plugins {
    id 'java-library'
    id 'com.gradle.build-scan' version '2.1'
    id 'org.shipkit.java' version '2.0.31'
    id 'com.github.sherter.google-java-format' version '0.8'
    id 'com.github.ben-manes.versions' version '0.20.0'
}

apply plugin: 'idea'

description = 'Java Tooling Framework'

repositories {
    mavenCentral()
}

dependencies {
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testRuntime "de.sormuras:brahms:${sormurasBrahmsVersion}"
}

compileJava {
    options.compilerArgs.addAll(['--release', '11'])
}

javadoc {
    classpath = project.sourceSets.main.compileClasspath
    options {
        memberLevel = JavadocMemberLevel.PROTECTED
        header = project.name
        use = true
        addBooleanOption('Xdoclint:html,syntax', true)
        addBooleanOption('html5', true)
        encoding = 'UTF-8'
        noTimestamp = true
    }
}

jar {
    exclude 'LICENSE' // keep root directory "clean", only `module-info.class` is allowed
    into 'de/sormuras/bartholdy', { from 'version.properties' }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

wrapper {
    gradleVersion = '5.0'
}
