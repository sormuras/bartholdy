plugins {
    id 'java-library'
    id 'com.gradle.build-scan' version '1.14'
    id 'org.shipkit.java' version '2.0.26'
    id 'com.github.sherter.google-java-format' version '0.7.1'
}

apply plugin: 'idea'

description = 'Java Tooling Framework'

idea {
    project {
        jdkName = '10'
        languageLevel = '10'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
}

javadoc {
    options {
        memberLevel = JavadocMemberLevel.PROTECTED
        header = project.name
        use = true
        addBooleanOption('Xdoclint:html,syntax', true)
        addBooleanOption('html5', true)
        encoding = 'UTF-8'
        noTimestamp = true
    }
}

jar {
    exclude 'LICENSE' // keep root directory "clean", only `module-info.class` is allowed
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

wrapper {
    gradleVersion = '4.8.1'
}
